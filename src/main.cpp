#include <Arduino.h>           // base p/ Arduino
#include <Wire.h>              // p/ comunicaci√≥n I2C
#include <SdFat.h>             // p/ tarjeta SD (Greiman)
#include <SPI.h>               // p/ comunicaci√≥n SPI
#include <INA226_WE.h>         // INA226 (Wollewald Electronics)
#include <LiquidCrystal_I2C.h> // p/ LCD I2C 16 x 2
#include <RtcDS1307.h>         // p/ RTC DS1307 / DS3231 (Makuna)

// --- CONFIGURACI√ìN PINES y SENSORES ---
// Creo instancia sensor INA226 c/ direcci√≥n p/ defecto (0x40)
INA226_WE ina(0x40);       // Direcci√≥n I2C INA226 base: 0x40 hasta 0x4F (16 direcciones posibles)
const int pinPulsador = 2; // PIN D2 p/ pulasor
const int pinRele = 3;     // PIN D3 p/ controlar rel√©

// --- PANTALLA LCD I2C ---
LiquidCrystal_I2C lcd(0x27, 16, 2); // Direcci√≥n t√≠pica: 0x27 / 16 columnas x 2 filas
bool lcdInicializado = false;

// --- SD ---
SdFat SD;       // p/ acceso SD
SdFile archivo; // p/ manejar archivos individuales

// --- VARIABLES ESTADO y TEMPORIZACI√ìN ---
bool medicionActiva = false; // Controla medici√≥n est√° activa o no con millis
bool releActivado = false;   // Estado rel√©
char nombreArchivo[7];       // Nombre logger "25.csv" ‚Üí ahorro RAM
// Variables p/ el temporizador de muestreo (10 [s])
unsigned long previousMillis = 0;       // Almacena √∫ltimo tiempo muestreo
unsigned long inicioMedicionMillis = 0; // Marca inicio medici√≥n
enum EstadoRele
{
    RELE_APAGADO,
    RELE_ENCENDIDO
};
EstadoRele estadoRele = RELE_APAGADO;
unsigned long tiempoCambioEstado = 0; // Marca √∫ltimo cambio estado rel√©
// --- CONSTANTES DE TIEMPO ---
const long interval = 10000;               // 10000 [ms] = 10 [s] intervalo muestreo ‚öôÔ∏è
const long releEnciendeMillis = 180000;    // 150000 [ms] = 3 [min] Tiempo medici√≥n p/ activar rel√© ‚öôÔ∏è
const long releTiempoEsperaMillis = 60000; // 60000 [ms] = 1 [min] tiempo espera p/ desactivar rel√© ‚öôÔ∏è
// --- Calibraci√≥n Corriente INA226 ---
const float offsetCorriente = -0.05; // Valor t√≠pico observado s/carga ‚öôÔ∏è
float corriente = 0.0;               // ‚úÖ Inicializaci√≥n segura

// --- INSTANCIA RTC ---
RtcDS1307<TwoWire> Rtc(Wire);

// --- P/ CORTE MEDICI√ìN
float ultimaTension = 0.0;
float penultimaTension = 0.0;

void setup()
{
    Wire.begin();       // Inicializa BUS I2C
    Serial.begin(9600); // Inicializa RS232 p/ monitoreo 9600 baudios
    lcd.init();         // Inicializa LCD
    lcd.backlight();    // Activa luz fondo
    lcd.clear();
    lcd.setCursor(0, 0);
    // Inicializa RTC
    Rtc.Begin();
    RtcDateTime now = Rtc.GetDateTime(); // Lectura Tiempo actual

    sprintf(nombreArchivo, "%02u.csv", now.Year() % 100); // Nombre archivo
    // Inicializa SD
    if (!SD.begin(10, SD_SCK_MHZ(4))) // Pin CS p/ m√≥dulo SD
    {
        Serial.println(F("‚ùå Fallo inicio SD"));
        lcd.setCursor(0, 1);
        lcd.clear();
        lcd.print(F("‚ùå Fallo inicio SD"));
        return; // Sale setup -> falla SD
    }
    Serial.println(F(" SD ‚úÖ!"));

    // CREO/SOBREESCRIBO NOMBRE ARCHIVO c/RTC
    // Solo escribo encabezado <-> archivo NO existe
    if (!SD.exists(nombreArchivo))
    {
        if (archivo.open(nombreArchivo, O_WRITE | O_CREAT))
        {
            // Establecer fecha/hora modificaci√≥n c/ RTC
            archivo.timestamp(T_CREATE, now.Year(), now.Month(), now.Day(), now.Hour(), now.Minute(), now.Second());
            archivo.timestamp(T_WRITE, now.Year(), now.Month(), now.Day(), now.Hour(), now.Minute(), now.Second());

            // Escribo encabezado
            archivo.println(F("FechaHora [RTC];Tiempo [s];Tension [V];Corriente [mA];Potencia [mW];Rele 1=activo 0=apagado"));

            // Validaciones antes de cerrar
            if (!archivo.isOpen())
            {
                Serial.println(F("‚ùå archivo no abierto."));
            }
            if (archivo.getWriteError())
            {
                Serial.println(F("‚ùå error de escritura en encabezado."));
                archivo.clearWriteError(); // limpia estado
            }

            archivo.close();

            Serial.print(F("Archivo creado c/ encabezado ‚úÖ: "));
            Serial.println(nombreArchivo);
        }
        else
        {
            Serial.println(F("‚ùå Error al crear archivo CSV."));
        }
    }
    else
    {
        Serial.print(F("Archivo existente: "));
        Serial.println(nombreArchivo);
    }

    // CONFIGURACI√ìN PINES:
    pinMode(pinPulsador, INPUT_PULLUP); // Pulsador activo en LOW
    pinMode(pinRele, OUTPUT);           // Salida p/ rel√©
    digitalWrite(pinRele, LOW);         // Rel√© apagado al inicio

    // --- Inicializo y Calibro sensor INA226 ---
    Wire.endTransmission();
    if (!ina.init())
    {
        Serial.println(F("‚ùå INA226 no conectado."));
        lcd.clear();
        lcd.setCursor(0, 0);
        lcd.print(F("INA226 ‚ùå"));
        lcd.setCursor(0, 1);
        lcd.print(F("Check I2C addr"));
        delay(5000); // Espera visible
        return;      // Sale setup s/ bloquear
    }
    // --- Calibraci√≥n sensor: ---
    // Rs  = 0.1 Œ© (shunt f√≠sico placa CJMCU-226)
    // Corriente m√°x esperada = 0.8 A ‚Üí tensi√≥n m√°x shunt ‚âà 80 mV
    // Asegura tensi√≥n Shunt no exceda 81.9 mV
    ina.setResistorRange(0.1);    //  Resistencia shunt 0.1 Œ© ‚öôÔ∏è
    ina.setCorrectionFactor(1.0); // Factor correcci√≥n ‚öôÔ∏è
    // Configurar tiempos conversi√≥n p/ mayor precisi√≥n:
    ina.setAverage(INA226_AVERAGE_16);            // Promedia 16 muestras (1, 4, 16, 64, 128, 256, 512, 1024) ‚öôÔ∏è
    ina.setConversionTime(INA226_CONV_TIME_4156); // Tiempo conversi√≥n ‚âà 4200 ¬µs ‚öôÔ∏è

    Serial.println(F("INA226 inicializado y calibrado para ‚âà 0.8 [A] con Rs = 0.1 [Œ©]."));

    // --- Mensaje Final LCD ---
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print(F("INA226 listo !"));
    lcd.setCursor(0, 1);
    lcd.print(F("Oprima pulsador"));

    Serial.println(F("Listo Presione pulsador p/ iniciar medici√≥n v 1.5"));
}

// *** FUNCI√ìN ARCHIVO CSV: ***
void registrarCSV(unsigned long tiempo, float tension, float corriente, float potencia, int estadoRele)
{
    File archivo = SD.open(nombreArchivo, FILE_WRITE);
    if (archivo)
    {
        // üïí Obtener fecha/hora actual del RTC
        RtcDateTime now = Rtc.GetDateTime();
        // Buffer para fecha/hora con dos d√≠gitos
        char fechaHora[20]; // 19+1 (4+5*2+2*-+1*" "=19)
        sprintf(fechaHora, "%04u-%02u-%02u %02u:%02u:%02u",
                now.Year(),
                now.Month(),
                now.Day(),
                now.Hour(),
                now.Minute(),
                now.Second());
        // Escribir fecha/hora absoluta
        archivo.print(fechaHora);
        archivo.print(";");
        // Escribir tiempo relativo y dem√°s variables
        archivo.print(tiempo); // segundos desde inicio
        archivo.print(";");

        // Tensi√≥n
        String valorTension = String(tension, 4);
        valorTension.replace('.', ',');
        archivo.print(valorTension);
        archivo.print(";");

        // Corriente
        String valorCorriente = String(corriente, 4);
        valorCorriente.replace('.', ',');
        archivo.print(valorCorriente);
        archivo.print(";");

        // Potencia
        String valorPotencia = String(potencia, 6);
        valorPotencia.replace('.', ',');
        archivo.print(valorPotencia);
        archivo.print(";");

        // Estado del rel√©
        archivo.println(estadoRele); // 1 = activo, 0 = apagado

        archivo.flush(); // aseguro escritura SD
        archivo.close();
    }
    else
    {
        Serial.println(F("‚ùå al escribir SD."));
    }
}

// *** FUNCI√ìN SALIDA LCD: ***
void mostrarLCD(float tension, float corriente, float potencia, unsigned long tiempo, bool releActivado)
{
    lcd.setCursor(0, 0);
    lcd.print(F("V"));
    lcd.print(tension, 3);
    lcd.print(F(" I"));
    lcd.print(corriente, 3);
    lcd.print(F("     ")); // ‚Üê relleno p/ limpiar residuos

    lcd.setCursor(0, 1);
    lcd.print(F("P"));
    lcd.print(potencia, 2);
    lcd.print(F("mW"));
    lcd.print(F(" t"));
    lcd.print(tiempo);
    lcd.print(F(" "));
    lcd.print(estadoRele);
    lcd.print(F("     ")); // ‚Üê relleno p/ limpiar residuos
}
void loop()
{
    // --------------------------------------------------------
    // Paso 1: LECTURA DE TENSI√ìN
    // Es la base p/ l√≥gica de inicio y detenci√≥n de la medici√≥n
    // --------------------------------------------------------
    float tension = ina.getBusVoltage_V(); // [V]
    // --------------------------------------------------------
    // Paso 2: L√ìGICA INICIO.
    // Solo activa <-> medici√≥n NO est√° activa y tensi√≥n >= 1,0 [V]
    // --------------------------------------------------------
    static unsigned long tiempoEstable = 0;
    if (!medicionActiva && digitalRead(pinPulsador) == LOW && tension >= 1.0) // ‚öôÔ∏è
    {
        if (millis() - tiempoEstable > 500) // tensi√≥n estable x 500 ms
        {
            medicionActiva = true;                 // Activa medici√≥n
            inicioMedicionMillis = millis();       // Marca inicio medici√≥n ‚ö†Ô∏è millis() se desborda (cada ~50 d√≠as)
            previousMillis = inicioMedicionMillis; // reinicia temporizador muestreo
            lcdInicializado = false;               // ‚Üê reinicia estado LCD
            Serial.println(F("Medici√≥n INICIADA x pulsador ‚úÖ"));
            delay(50); // Retardo p/ evitar rebotes pulsador
        }
    }
    else
    {
        tiempoEstable = millis(); // reinicia si no cumple condici√≥n
    }

    // --- L√≥gica Activaci√≥n / Apagado rel√© ---
    if (medicionActiva)
    {
        unsigned long tiempoActual = millis();
        unsigned long tiempoTranscurrido = tiempoActual - tiempoCambioEstado;

        if (estadoRele == RELE_APAGADO && tiempoTranscurrido >= releEnciendeMillis)
        {
            // Encender rel√©
            digitalWrite(pinRele, HIGH);
            estadoRele = RELE_ENCENDIDO;
            tiempoCambioEstado = tiempoActual;
            Serial.println(F("üîî Rel√© ENCENDIDO (inicio ciclo medici√≥n Voc)"));
        }

        else if (estadoRele == RELE_ENCENDIDO && tiempoTranscurrido >= releTiempoEsperaMillis)
        {
            // Apagar rel√©
            digitalWrite(pinRele, LOW);
            estadoRele = RELE_APAGADO;
            tiempoCambioEstado = tiempoActual;
            Serial.println(F("‚èπÔ∏è Rel√© APAGADO (tiempo medici√≥n V, I y P)"));
        }
    }

    // --------------------------------------------------------
    // Paso 3: L√ìGICA DETENCI√ìN.
    // Si la medici√≥n est√° activa, el rel√© encendido y la tensi√≥n es < 1,0 [V], se detiene
    // --------------------------------------------------------
    if (medicionActiva && estadoRele == RELE_ENCENDIDO)
    {
        // Verifica √∫ltima y pen√∫ltima medici√≥n
        if (ultimaTension < 1.0 && penultimaTension < 1.0) // ‚öôÔ∏è Tensi√≥n detenci√≥n
        {

            Serial.println(F("‚ö†Ô∏è Tensi√≥n < 1,0 [V] c/ Rel√© activo. Medici√≥n detenida.")); // Alerta
            medicionActiva = false;                                                      // Desactiva medici√≥n
            previousMillis = 0;                                                          // Reinicia contador
            inicioMedicionMillis = 0;                                                    // Reinicia marcador inicio
            lcdInicializado = false;                                                     // ‚Üê preparo LCD p/ borrado √∫nico pr√≥x. medici√≥n

            // üîÅ Apaga rel√© y reinicio estado
            digitalWrite(pinRele, LOW);
            estadoRele = RELE_APAGADO;
            tiempoCambioEstado = 0;

            // üïí Obtener fecha/hora actual RTC
            RtcDateTime now = Rtc.GetDateTime();

            // üìù Abrir archivo y registrar fin
            if (archivo.open(nombreArchivo, O_WRITE | O_APPEND))
            {
                archivo.println(F("Fin de medicion"));

                // üïí Actualizar fecha de modificaci√≥n
                archivo.timestamp(T_WRITE, now.Year(), now.Month(), now.Day(), now.Hour(), now.Minute(), now.Second());

                archivo.flush();
                archivo.close();
            }
            else
            {
                Serial.println(F("‚ùå No se pudo abrir archivo p/ registrar fin"));
            }

            // üñ•Ô∏è Actualizar LCD
            lcd.clear();
            lcd.setCursor(0, 0);
            lcd.print(F("Medicion X OFF"));
            lcd.setCursor(0, 1);
            lcd.print(F("Tension < 1,0 V"));
            // üß≠ Mensaje serie
            Serial.println(F("Sistema termin√≥ medici√≥n completa de descarga"));
        }
    }
    // --------------------------------------------------------
    // Paso 4: L√ìGICA DE MEDICI√ìN PERI√ìDICA
    // Solo si la medici√≥n est√° activa, y han transcurrido 10 s[s]
    // --------------------------------------------------------
    unsigned long currentMillis = millis();
    unsigned long tiempo = (millis() - inicioMedicionMillis) / 1000;    // Tiempo [s] desde inicio c/ medici√≥n
    if (medicionActiva && (currentMillis - previousMillis >= interval)) // interval = 10000 [ms]
    {
        previousMillis = currentMillis; // Actualiza √∫ltimo tiempo temporizador (muestreo)
        if (!lcdInicializado)
        {
            lcd.clear(); // ‚Üê solo 1 vez
            lcdInicializado = true;
        }
        // Lectura tensi√≥n [V]
        tension = ina.getBusVoltage_V();
        // Actualizar pen√∫ltima y √∫ltima tensi√≥n
        penultimaTension = ultimaTension;
        ultimaTension = tension;
        // Lectura corriente [mA]]:
        corriente = ina.getCurrent_mA() - offsetCorriente; // [mA]
        // Lectura Potencia [mW]]:
        float potencia = ina.getBusPower(); // [mW]

        // --------------------------------------------------------
        // ‚¨áÔ∏è FORMATO DE SALIDA ‚¨áÔ∏è
        // --------------------------------------------------------
        // ===        Salida Formato legible puerto serie       ===

        Serial.println(F("=== Medici√≥n INA226 ==="));
        // Muestra valores x puerto serie
        Serial.print(F("Tiempo [s]: "));
        Serial.println(tiempo); // Muestra tiempo
        Serial.print(F("Tensi√≥n [V]: "));
        Serial.println(tension, 3); // Muestra tensi√≥n [V]
        Serial.print(F("Corriente [mA]: "));
        Serial.println(corriente, 3); // Muestra corriente [mA]
        Serial.print(F("Potencia [mW]: "));
        Serial.println(potencia, 5); // Muestra potencia [mW]
        Serial.print(F("Rel√© [0-1]: "));
        Serial.println(estadoRele); // Estado Rel√©

        // --------------------------------------------------------
        // === VERSI√ìN 1.5: Salida SD formato CSV  ===
        registrarCSV(tiempo, tension, corriente, potencia, estadoRele == RELE_ENCENDIDO ? 1 : 0); // Funci√≥n CSV
        // === SALIDA LCD 16 x 2 ===
        // --------------------------------------------------------
        mostrarLCD(tension, corriente, potencia, tiempo, estadoRele == RELE_ENCENDIDO ? 1 : 0); // Funci√≥n LCD
    }
}